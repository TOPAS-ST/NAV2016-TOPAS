OBJECT Page 9100 Purchase Line FactBox
{
  OBJECT-PROPERTIES
  {
    Date=11.01.18;
    Time=13:55:15;
    Modified=Yes;
    Version List=NAVW19.00,TOP1.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Einkaufszeilendetails;
               ENU=Purchase Line Details];
    SourceTable=Table39;
    PageType=CardPart;
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 8   ;1   ;Field     ;
                Lookup=No;
                CaptionML=[DEU=Artikelnr.;
                           ENU=Item No.];
                SourceExpr="No.";
                OnDrillDown=BEGIN
                              ShowDetails;
                            END;
                             }

    { 3   ;1   ;Field     ;
                Name=Availability;
                DrillDown=Yes;
                CaptionML=[DEU=Verfgbarkeit;
                           ENU=Availability];
                DecimalPlaces=2:0;
                SourceExpr=STRSUBSTNO('%1',PurchInfoPaneMgt.CalcAvailability(Rec));
                Editable=TRUE;
                OnDrillDown=BEGIN
                              ItemAvailFormsMgt.ShowItemAvailFromPurchLine(Rec,ItemAvailFormsMgt.ByEvent);
                              CurrPage.UPDATE(TRUE);
                            END;
                             }

    { 5   ;1   ;Field     ;
                DrillDown=Yes;
                CaptionML=[DEU=Einkaufspreise;
                           ENU=Purchase Prices];
                SourceExpr=STRSUBSTNO('%1',PurchInfoPaneMgt.CalcNoOfPurchasePrices(Rec));
                Editable=TRUE;
                OnDrillDown=BEGIN
                              ShowPrices;
                              CurrPage.UPDATE;
                            END;
                             }

    { 11  ;1   ;Field     ;
                DrillDown=Yes;
                CaptionML=[DEU=Einkaufszeilenrabatte;
                           ENU=Purchase Line Discounts];
                SourceExpr=STRSUBSTNO('%1',PurchInfoPaneMgt.CalcNoOfPurchLineDisc(Rec));
                Visible=FALSE;
                Editable=TRUE;
                OnDrillDown=BEGIN
                              ShowLineDisc;
                              CurrPage.UPDATE;
                            END;
                             }

    { 1000000000;1;Field  ;
                Name=LookupSalesOrders;
                CaptionML=[DEU=Menge in Auftrag;
                           ENU=Qty. on Sales Order];
                SourceExpr=STRSUBSTNO('%1',PurchInfoPaneMgt.CalcNoQtyOnSalesOrder(Rec));
                OnDrillDown=BEGIN
                              //TOPAS 11.01.18 ST
                              ShowSalesOrders;
                              //TOPAS Ende
                            END;
                             }

    { 1000000001;1;Field  ;
                Name=LookupItemTariffNo;
                CaptionML=[DEU=Zollpos.;
                           ENU=Tariff No.];
                SourceExpr=STRSUBSTNO('%1',PurchInfoPaneMgt.GetItemTariffNo(Rec));
                OnDrillDown=BEGIN
                              //TOPAS 11.01.18 ST
                              ShowtemTariffNo;
                              //TOPAS Ende
                            END;
                             }

  }
  CODE
  {
    VAR
      PurchHeader@1000 : Record 38;
      PurchPriceCalcMgt@1002 : Codeunit 7010;
      PurchInfoPaneMgt@1001 : Codeunit 7181;
      ItemAvailFormsMgt@1003 : Codeunit 353;
      "***TOPAS_Var***"@1000000000 : Integer;
      SalesLine@1000000001 : Record 37;
      TariffNumber@1000000002 : Record 260;

    LOCAL PROCEDURE ShowDetails@17();
    VAR
      Item@1000 : Record 27;
    BEGIN
      IF Type = Type::Item THEN BEGIN
        Item.GET("No.");
        PAGE.RUN(PAGE::"Item Card",Item);
      END;
    END;

    LOCAL PROCEDURE ShowPrices@15();
    BEGIN
      PurchHeader.GET("Document Type","Document No.");
      CLEAR(PurchPriceCalcMgt);
      PurchPriceCalcMgt.GetPurchLinePrice(PurchHeader,Rec);
    END;

    LOCAL PROCEDURE ShowLineDisc@16();
    BEGIN
      PurchHeader.GET("Document Type","Document No.");
      CLEAR(PurchPriceCalcMgt);
      PurchPriceCalcMgt.GetPurchLineLineDisc(PurchHeader,Rec);
    END;

    LOCAL PROCEDURE ShowSalesOrders@1000000008();
    BEGIN
      //TOPAS 11.01.18 ST - Zeigt die Artikel in offenen Auftr„gen
      IF Type = Type::Item THEN BEGIN
        SalesLine.SETRANGE("Document Type",SalesLine."Document Type"::Order);
        SalesLine.SETRANGE(Type,SalesLine.Type::Item);
        SalesLine.SETRANGE("No.","No.");
        PAGE.RUN(PAGE::"Sales Lines",SalesLine);
      END;
      //TOPAS Ende
    END;

    LOCAL PROCEDURE ShowtemTariffNo@1000000009();
    BEGIN
      //TOPAS 11.01.18 ST - Anzeige der Warentarifnummern mit Beschreibung
      IF Type = Type::Item THEN BEGIN
        TariffNumber.SETPOSITION('No.=CONST(' + STRSUBSTNO('%1',PurchInfoPaneMgt.GetItemTariffNo(Rec)) + ')');
        PAGE.RUN(PAGE::"Tariff Numbers",TariffNumber);
      END;
      //TOPAS Ende
    END;

    BEGIN
    {
      Lfd.Nr. Version   Datum       User  Beschreibung
      ===========================================================================
      1       TOP1.00   18.01.18    ST    Feld "Purchase Line Discounts" ausgeblendet, neu "Menge in Auftrag" und Zollpos, jeweils mit Drilldown
    }
    END.
  }
}

