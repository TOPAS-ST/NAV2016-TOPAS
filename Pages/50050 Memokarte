OBJECT Page 50050 Memokarte
{
  OBJECT-PROPERTIES
  {
    Date=06.11.18;
    Time=16:37:21;
    Modified=Yes;
    Version List=TOPAS;
  }
  PROPERTIES
  {
    InsertAllowed=No;
    SourceTable=Table50003;
    DataCaptionExpr=Caption;
    PageType=Document;
    RefreshOnActivate=Yes;
    OnOpenPage=BEGIN
                 UserTab.RESET;
                 UserTab.SETCURRENTKEY("User Name");
                 UserTab.SETRANGE("User Name",USERID);
                 UserTab.FINDFIRST;
                 IF (UserTab."Salesperson Code" = '') THEN
                   ERROR(Text003,UserTab.FIELDCAPTION("User Name"),USERID,UserTab.FIELDCAPTION("Salesperson Code"),UserTab.TABLECAPTION);

                 show_change_campaign_field := FALSE;
               END;

    OnAfterGetRecord=BEGIN
                       IF "Kampagnennr." <> '' THEN BEGIN
                         Projekte.RESET;
                         Projekte.SETRANGE(Projekte."No.","Kampagnennr.");
                         IF Projekte.FINDFIRST THEN
                           Projektname := Projekte.Description
                         ELSE
                           Projektname := '';
                       END ELSE
                         IF "Mandantennr." <> '' THEN BEGIN
                           Cont.RESET;
                           Cont.SETRANGE(Cont."No.","Mandantennr.");
                           IF Cont.FINDFIRST THEN
                             Kontaktname := Cont.Name
                           ELSE
                             Kontaktname := '';
                         END;
                     END;

    OnModifyRecord=BEGIN
                     "Korrigiert von" := USERID;
                     "Korrigiert am"  := WORKDATE;
                   END;

    OnDeleteRecord=BEGIN
                     CurrPage.CLOSE;
                   END;

    OnQueryClosePage=BEGIN
                       IF (Memodatum = 0D) AND (Memokurztext <> '') THEN
                         ERROR(Text002);

                       IF ("Memonr." <> '') AND ("Kampagnennr." <> '') THEN BEGIN
                         IF UPPERCASE(UserTab."Salesperson Code") = "Angelegt von" THEN BEGIN
                           Memokopf.RESET;
                           Memokopf.SETRANGE("Angelegt von",UserTab."Salesperson Code");
                           Memokopf.SETRANGE("Kampagnennr.","Kampagnennr.");
                           Memokopf.SETRANGE(Memokopf.Anlagedatum,WORKDATE);
                           IF Memokopf.FINDFIRST THEN
                             REPEAT
                               IF Memokopf."Memonr." = "Memonr." THEN BEGIN
                                 ToDo.RESET;
                                 ToDo.SETRANGE(ToDo."Salesperson Code",UserTab."Salesperson Code");
                                 ToDo.SETRANGE(ToDo."Campaign No.","Kampagnennr.");
                                 ToDo.SETRANGE(ToDo.Closed,FALSE);
                                 ToDo.SETFILTER(ToDo.Date,'>%1',WORKDATE);
                                 IF NOT ToDo.FINDFIRST THEN
                                   IF CONFIRM(Text000,TRUE) THEN
                                     CreateToDo;
                               END;
                             UNTIL Memokopf.NEXT = 0;
                         END;
                       END;

                       IF ("Memonr." <> '') AND ("Mandantennr." <> '') THEN BEGIN
                         IF UPPERCASE(UserTab."Salesperson Code") = "Angelegt von" THEN BEGIN
                           Memokopf.RESET;
                           Memokopf.SETRANGE(Memokopf."Angelegt von",UserTab."Salesperson Code");
                           Memokopf.SETRANGE(Memokopf."Mandantennr.","Mandantennr.");
                           Memokopf.SETRANGE(Memokopf.Anlagedatum,WORKDATE);
                           IF Memokopf.FINDFIRST THEN
                             REPEAT
                               IF Memokopf."Memonr." = "Memonr." THEN BEGIN
                                 ToDo.RESET;
                                 ToDo.SETRANGE(ToDo.Ersteller,UserTab."Salesperson Code");
                                 ToDo.SETRANGE(ToDo."Contact Company No.","Mandantennr.");
                                 ToDo.SETRANGE(ToDo."Last Date Modified",WORKDATE);
                                 IF NOT ToDo.FINDFIRST THEN
                                   IF CONFIRM(Text001,TRUE) THEN
                                     CreateToDo;
                               END;
                             UNTIL Memokopf.NEXT = 0;
                         END;
                       END;
                     END;

    ActionList=ACTIONS
    {
      { 1900000000;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 1000000008;1 ;ActionGroup;
                      CaptionML=DEU=Funktionen;
                      ActionContainerType=NewDocumentItems }
      { 1000000005;2 ;Action    ;
                      Name=ChangeCampaign;
                      CaptionML=DEU=Kampagnennr. „ndern;
                      Promoted=Yes;
                      Image=Campaign;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 IF show_change_campaign_field = FALSE THEN BEGIN
                                   show_change_campaign_field := TRUE;
                                   CurrPage.UPDATE;
                                   MESSAGE(Text004,FIELDCAPTION("Kampagnennr."),'Kampagnennr. „ndern');
                                 END ELSE BEGIN
                                   IF new_campaignno = '' THEN BEGIN
                                     show_change_campaign_field := FALSE;
                                     CurrPage.UPDATE;
                                   END ELSE BEGIN
                                     IF "Kampagnennr." = new_campaignno THEN ERROR(Text007,FIELDCAPTION("Kampagnennr."));
                                     IF CONFIRM(Text005,TRUE,FIELDCAPTION("Kampagnennr.")) THEN BEGIN
                                       IF (Projekte.GET(new_campaignno)) AND (new_campaignno <> '') THEN BEGIN
                                         Memokopf.GET("Memonr.","Kampagnennr.","Mandantennr.");
                                         CurrPage.SAVERECORD;
                                         CurrPage.CLOSE;
                                         Memokopf.RENAME(Memokopf."Memonr.",new_campaignno,'');
                                         MESSAGE(Text006);
                                       END;
                                     END;
                                   END;
                                 END;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000;0;Container ;
                ContainerType=ContentArea }

    { 1900003;1;Group     ;
                GroupType=Group }

    { 1140001;2;Field     ;
                SourceExpr="Memonr.";
                Editable=FALSE }

    { 1140005;2;Field     ;
                SourceExpr=Memokurztext }

    { 1140011;2;Field     ;
                SourceExpr=Kontaktart }

    { 1140000;2;Field     ;
                SourceExpr=Memodatum }

    { 1140003;2;Field     ;
                SourceExpr="Kampagnennr.";
                Editable=FALSE }

    { 1140022;2;Field     ;
                CaptionML=DEU=Projektname;
                SourceExpr=Projektname;
                Editable=FALSE }

    { 1140013;2;Field     ;
                SourceExpr="Mandantennr.";
                Editable=FALSE }

    { 1140025;2;Field     ;
                CaptionML=DEU=Kontaktname;
                SourceExpr=Kontaktname;
                Editable=FALSE }

    { 1140015;2;Field     ;
                SourceExpr=Anlagedatum;
                Editable=FALSE }

    { 1140017;2;Field     ;
                SourceExpr="Angelegt von";
                Editable=FALSE }

    { 1140019;2;Field     ;
                SourceExpr="Korrigiert am";
                Editable=FALSE }

    { 1140021;2;Field     ;
                SourceExpr="Korrigiert von";
                Editable=FALSE }

    { 1140026;2;Field     ;
                SourceExpr=QBR }

    { 1140028;2;Field     ;
                SourceExpr=ISO }

    { 1000000006;1;Group  ;
                CaptionML=DEU=Zusatz;
                Visible=show_change_campaign_field;
                GroupType=Group }

    { 1000000007;2;Field  ;
                CaptionML=DEU=Neue Kampagnennr.;
                SourceExpr=new_campaignno;
                TableRelation=Campaign }

    { 1000000004;1;Part   ;
                CaptionML=DEU=Memozeilen;
                SubPageLink=Memonr.=FIELD(Memonr.);
                PagePartID=Page50051;
                PartType=Page }

    { 1000000001;;Container;
                ContainerType=FactBoxArea }

    { 1000000000;1;Part   ;
                SubPageLink=Memonr.=FIELD(Memonr.);
                PagePartID=Page50049;
                PartType=Page }

    { 1000000002;1;Part   ;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1000000003;1;Part   ;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Projekte@1140000 : Record 5071;
      Cont@1140001 : Record 5050;
      Memokopf@1000000003 : Record 50003;
      ToDo@1000000002 : Record 5080;
      UserTab@1000000000 : Record 2000000120;
      Projektname@1140002 : Text[50];
      Kontaktname@1140003 : Text[50];
      Text000@1140007 : TextConst 'DEU=M”chten Sie jetzt eine Aufgabe zu diesem Projekt erstellen?';
      Text001@1140006 : TextConst 'DEU=M”chten Sie jetzt eine Aufgabe zu diesem Kontakt erstellen?';
      Text002@1140008 : TextConst 'DEU=Bitte tragen Sie das Memodatum ein.';
      Text003@1000000001 : TextConst 'DEU=Fehlende Zuordnung zwischen %1 (%2) und %3 in der Tabelle %4. Bitte wenden Sie sich an Ihren Systemadministrator.';
      new_campaignno@1000000004 : Code[20];
      show_change_campaign_field@1000000005 : Boolean;
      Text004@1000000006 : TextConst 'DEU=Das Feld zum „ndern der %1 ist nun verfgbar. Bitte geben Sie die neue %1 an und best„tigen die Funktion %2 erneut.';
      Text005@1000000007 : TextConst 'DEU=Wenn Sie den Vorgang fortsetzen, muss die Memokarte geschlossen werden. M”chten Sie die %1 Zuordnung jetzt „ndern?';
      Text006@1000000008 : TextConst 'DEU=Das Verschieben des Memos wurde erfolgreich abgeschlossen.';
      Text007@1000000009 : TextConst 'DEU=Keine Transaktion m”glich, die %1 stimmt berein.';

    PROCEDURE Caption@1140000() : Text[260];
    VAR
      CaptionStr@1140000 : Text[260];
    BEGIN
      IF Cont.GET("Mandantennr.") THEN
        CaptionStr := COPYSTR("Memonr." + ' ' + Memokurztext + ' ' + "Mandantennr." + ' ' + Cont.Name,1,MAXSTRLEN(CaptionStr));
      IF Projekte.GET("Kampagnennr.") THEN
        CaptionStr := COPYSTR("Memonr." + ' ' + Memokurztext + ' ' + "Kampagnennr." + ' ' + Projekte.Description,1,MAXSTRLEN(CaptionStr));

      EXIT(CaptionStr);
    END;

    BEGIN
    END.
  }
}

